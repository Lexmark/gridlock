/*
 * Copyright 2016 Lexmark International Technology S.A.  All rights reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0. If a copy of the MPL was not distributed with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "gridlock-core"

dependencies {
    compile 'org.codehaus.groovy:groovy:2.4.3'
    compile 'com.google.guava:guava:16.0.1'
    compile 'org.slf4j:slf4j-api:1.7.6'

    testCompile group: 'junit', name:'junit', version:'4.10'
    testRuntime 'ch.qos.logback:logback-core:1.1.1'
    testRuntime 'ch.qos.logback:logback-classic:1.1.1'
}

compileJava {
    options.encoding = 'UTF-8'
}

test {
    useJUnit()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gridlock Core', 'Implementation-Version': version

        excludes.addAll(["META-INF\\**\\*.SF", "META-INF\\**\\*.RSA"])
    }

    inputs.files configurations.runtime
}

jar.doFirst {
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

build.dependsOn(sourcesJar, javadocJar)


//maven central / sonatype config
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gridlock'
                packaging 'jar'
                description 'A load driver framework.'
                url 'http://GitHub.com/Lexmark/gridlock'

                scm {
                    url 'scm:git@github.com:Lexmark/gridlock.git'
                    connection 'scm:git@github.com:Lexmark/gridlock.git'
                    developerConnection 'scm:git@github.com:Lexmark/gridlock.git'
                }

                licenses {
                    license {
                        name 'Mozilla Public License, v. 2.0'
                        url 'http://mozilla.org/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id 'lexmark-benmarks'
                        name 'Ben Marks'
                        email 'ben.marks@lexmark.com'
                    }
                }
            }
        }
    }
}

